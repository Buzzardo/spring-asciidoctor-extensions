buildscript {
	repositories {
		mavenCentral()
		mavenLocal()
	}
	dependencies {
		classpath "io.spring.javaformat:spring-javaformat-gradle-plugin:$springJavaFormatVersion"
	}
}

plugins {
	id 'checkstyle'
	id 'com.jfrog.artifactory' version '4.4.13'
	id 'java'
	id 'maven'
	id 'io.spring.nohttp' version '0.0.3.RELEASE'
}

group = 'io.spring.asciidoctor'
description = 'Spring Asciidoctor Extensions'

apply from: 'gradle/publish-maven.gradle'
apply plugin: 'io.spring.javaformat'
apply plugin: 'io.spring.compatibility-test'

repositories {
	mavenCentral()
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
	checkstyle "io.spring.javaformat:spring-javaformat-checkstyle:$springJavaFormatVersion"
	compileOnly "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	testCompile "org.asciidoctor:asciidoctorj:$asciidoctorjVersion"
	testCompile 'junit:junit:4.12'
	testCompile 'org.assertj:assertj-core:2.6.0'
}

nohttp {
	source.exclude "buildSrc/**"
}

compatibilityTest {
	dependency('AsciidoctorJ') { asciidoctorj ->
		asciidoctorj.groupId = 'org.asciidoctor'
		asciidoctorj.artifactId = 'asciidoctorj'
		asciidoctorj.versions = [ '1.6.2' ]
	}
}

javadoc {
	title = "$project.description $project.version API"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
	classifier = "javadoc"
	from javadoc
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

artifactory {
	contextUrl = 'https://repo.spring.io'
	publish {
		repository {
			repoKey = 'libs-snapshot-local'
			if (project.hasProperty('artifactoryUsername')
					&& project.hasProperty('artifactoryPassword')) {
				username = artifactoryUsername
				password = artifactoryPassword
			}
		}
	}
}

artifactoryPublish {
	publishConfigs 'archives'
	publishIvy false
}